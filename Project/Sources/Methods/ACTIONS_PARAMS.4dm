//%attributes = {"invisible":true}
var $e; $Æ’ : Object

$Æ’:=panel_Definition

If (FORM Event:C1606.objectName=Null:C1517)  // <== FORM METHOD
	
	$e:=panel_Form_common(On Load:K2:1; On Timer:K2:25)
	
	Case of 
			
			//______________________________________________________
		: ($e.code=On Load:K2:1)
			
			androidLimitations(True:C214)
			
			// This trick remove the horizontal gap
			$Æ’.parameters.setScrollbars(0; 2)
			
			// Set the initial display
			If ($Æ’.action#Null:C1517)
				
				$Æ’.add.enable()
				
				If (_and(Formula:C1597(Form:C1466.actions.parameters#Null:C1517); Formula:C1597(Form:C1466.actions.parameters.length>0)))
					
					// Select last used action (or the first one)
					If ($Æ’.$current#Null:C1517)
						
						var $indx : Integer
						$indx:=Form:C1466.actions.parameters.indexOf($Æ’.$current)
						$Æ’.parameters.select($indx+1)
						
					Else 
						
						$Æ’.parameters.select(1)
						
					End if 
					
					//$Æ’.callMeBack("selectParameters")
					//$Æ’.updateParameters()
					
					//$Æ’.actions.focus()
					
				End if 
				
			Else 
				
				$Æ’.noSelection.show()
				$Æ’.noTable.hide()
				$Æ’.withSelection.hide()
				$Æ’.noParameters.hide()
				
			End if 
			
			// Add the events that we cannot select in the form properties ðŸ˜‡
			$Æ’.appendEvents(New collection:C1472(On Alternative Click:K2:36))
			
			//______________________________________________________
		: ($e.code=On Timer:K2:25)
			
			$Æ’.update()
			
			//______________________________________________________
	End case 
	
Else   // <== WIDGETS METHOD
	
	$e:=$Æ’.event
	
	Case of 
			
			//==============================================
		: ($Æ’.parameters.catch())
			
			Case of 
					
					//_____________________________________
				: ($e.code=On Getting Focus:K2:7)
					
					$Æ’.parameters.setColors(Foreground color:K23:1)
					$Æ’.parametersBorder.setColors(EDITOR.selectedColor)
					
					//_____________________________________
				: ($e.code=On Losing Focus:K2:8)
					
					$Æ’.parameters.setColors(Foreground color:K23:1)
					$Æ’.parametersBorder.setColors(EDITOR.backgroundUnselectedColor)
					
					//_____________________________________
				: ($e.code=On Selection Change:K2:29)
					
					$Æ’.$current:=$Æ’.current
					
					// Update UI
					$Æ’.refresh()
					
					//_____________________________________
				: (editor_Locked)\
					 | (Num:C11($e.row)=0)
					
					// <NOTHING MORE TO DO>
					
					//_____________________________________
				: ($e.code=On Begin Drag Over:K2:44)
					
					$Æ’.doBeginDrag()
					
					//_____________________________________
				: ($e.code=On Drop:K2:12)
					
					$Æ’.doOnDrop()
					
					//_____________________________________
				: ($e.code=On Mouse Leave:K2:34)
					
					$Æ’.dropCursor.hide()
					
					//_____________________________________
			End case 
			
			//==============================================
		: ($Æ’.add.catch($e; On Clicked:K2:4))
			
			$Æ’.doNewParameter()
			
			//==============================================
		: ($Æ’.add.catch($e; On Alternative Click:K2:36))
			
			$Æ’.doAddParameterMenu()
			
			//==============================================
		: ($Æ’.remove.catch($e; On Clicked:K2:4))
			
			$Æ’.doRemoveParameter()
			
			//==============================================
		: ($Æ’.mandatory.catch($e; On Clicked:K2:4))
			
			$Æ’.doMandatory()
			
			//==============================================
		: ($Æ’.formatPopup.catch($e; On Clicked:K2:4))
			
			$Æ’.doFormatMenu()
			
			//==============================================
		: ($Æ’.min.catch($e; On Data Change:K2:15))
			
			$Æ’.doRule("min")
			
			//==============================================
		: ($Æ’.max.catch($e; On Data Change:K2:15))
			
			$Æ’.doRule("max")
			
			//==============================================
		: ($Æ’.defaultValue.catch($e; On After Edit:K2:43))
			
			If (Length:C16(Get edited text:C655)=0)
				
				OB REMOVE:C1226($Æ’.current; "default")
				
			End if 
			
			//==============================================
		: ($Æ’.defaultValue.catch($e; On Data Change:K2:15))
			
			$Æ’.doDefaultValue()
			
			//==================================================
		: ($e.code=On Data Change:K2:15)\
			 & ($Æ’.linked.belongsTo($e.objectName))  // Linked widgets
			
			PROJECT.save()
			
			//==============================================
	End case 
End if 
