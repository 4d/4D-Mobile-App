//%attributes = {"invisible":true}
/*
Long Integer := ***ACTIONS_PARAMS_OBJECTS_HANDLER***
________________________________________________________

*/
  // ----------------------------------------------------
  // Project method : ACTIONS_PARAMS_OBJECTS_HANDLER
  // Database: 4D Mobile Express
  // ID[35D81378EF494DE38795C6B491E4CAA8]
  // Created #11-4-2019 by Vincent de Lachaux
  // ----------------------------------------------------
  // Description:
  //
  // ----------------------------------------------------
  // Declarations
C_LONGINT:C283($0)

C_LONGINT:C283($Lon_parameters)
C_OBJECT:C1216($o;$Obj_context;$Obj_form;$Obj_menu)

If (False:C215)
	C_LONGINT:C283(ACTIONS_PARAMS_OBJECTS_HANDLER ;$0)
End if 

  // ----------------------------------------------------
  // Initialisations
$Lon_parameters:=Count parameters:C259

If (Asserted:C1132($Lon_parameters>=0;"Missing parameter"))
	
	  // NO PARAMETERS REQUIRED
	
	  // Optional parameters
	If ($Lon_parameters>=1)
		
		  // <NONE>
		
	End if 
	
	$Obj_form:=ACTIONS_PARAMS_Handler (New object:C1471(\
		"action";"init"))
	
	$Obj_context:=$Obj_form.$
	
Else 
	
	ABORT:C156
	
End if 

  // ----------------------------------------------------
Case of 
		
		  //==================================================
	: ($Obj_form.form.currentWidget=$Obj_form.parameters.name)  // Parameters listbox
		
		$Obj_form.parameters.get()
		
		Case of 
				
				  //______________________________________________________
			: ($Obj_form.form.event=On Display Detail:K2:22)
				
				  // <Generated by Background Color Expression>
				
				  //______________________________________________________
			: ($Obj_form.form.event=On Getting Focus:K2:7)\
				 | ($Obj_form.form.event=On Losing Focus:K2:8)
				
				$Obj_context.listUI()
				
				  //______________________________________________________
			: ($Obj_form.form.event=On Selection Change:K2:29)
				
				$Obj_form.form.refresh()
				
				  //______________________________________________________
			: (editor_Locked )
				
				  // <NOTHING MORE TO DO>
				
				  //______________________________________________________
			: ($Obj_form.parameters.row=0)
				
				  // <NOTHING MORE TO DO>
				
				  //______________________________________________________
			Else 
				
				ASSERT:C1129(False:C215;"Form event activated unnecessarily ("+String:C10($Obj_form.form.event)+")")
				
				  //______________________________________________________
		End case 
		
		  //==================================================
	: ($Obj_form.form.currentWidget=$Obj_form.typeMenu.name)  // Types choice
		
		$Obj_menu:=ui.menu()
		$Obj_menu.append(":xliff:text";"string";$Obj_context.parameter.type="string")
		$Obj_menu.append(":xliff:number";"number";$Obj_context.parameter.type="number")
		$Obj_menu.append(":xliff:boolean";"bool";$Obj_context.parameter.type="bool")
		$Obj_menu.append(":xliff:date";"date";$Obj_context.parameter.type="date")
		$Obj_menu.append(":xliff:time";"time";$Obj_context.parameter.type="time")
		$Obj_menu.append(":xliff:picture";"image";$Obj_context.parameter.type="image")
		
		  // Position according to the box
		$o:=$Obj_form.typeBorder.getCoordinates()
		$Obj_menu.popup("";$o.windowCoordinates.left;$o.windowCoordinates.bottom)
		$Obj_menu.clear()
		
		If ($Obj_menu.choice#"")
			
			$Obj_context.parameter.type:=$Obj_menu.choice
			
		End if 
		
		  //==================================================
	Else 
		
		ASSERT:C1129(False:C215;"Unknown widget: \""+String:C10($Obj_form.form.currentWidget)+"\"")
		
		  //==================================================
End case 

If (featuresFlags.with(8858))
	
	project.save()
	
End if 

  // ----------------------------------------------------
  // Return
  // ----------------------------------------------------
  // End