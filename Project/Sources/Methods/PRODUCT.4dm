//%attributes = {"invisible":true}
// ----------------------------------------------------
// Project method : PRODUCT
// Created 16-9-2020 by Vincent de Lachaux
// ----------------------------------------------------
// Description:
// PRODUCT pannel management
// ----------------------------------------------------
// Declarations
var $e; $Æ’ : Object

// ----------------------------------------------------
// Initialisations
$Æ’:=panel_Definition

// ----------------------------------------------------
If (FORM Event:C1606.objectName=Null:C1517)  // <== FORM METHOD
	
	$e:=panel_Form(On Load:K2:1; On Timer:K2:25; On Bound Variable Change:K2:52)
	
	Case of 
			
			//______________________________________________________
		: ($e.code=On Load:K2:1)
			
			If (FEATURE.with("targetPannel"))
				
				$Æ’.os.hide()
				$Æ’.preview.hide()
				$Æ’.dominantColor.moveVertically(-125)
				
			Else 
				
				If (FEATURE.with("androidBeta"))\
					 | (Bool:C1537(PROJECT.$android))  // ðŸš§
					
					If (Is Windows:C1573)
						
						If (Form:C1466.$ios)
							
							$Æ’.android.setPicture("#images/os/Android-32.png")\
								.setBackgroundPicture()\
								.setNumStates(1)
							
							$Æ’.preview.show()
							
							$Æ’.ios.disable()
							
							$Æ’.ios.setPicture("#images/os/iOS-32.png")\
								.setBackgroundPicture()\
								.setNumStates(1)
							
						Else 
							
							$Æ’.os.hide()
							$Æ’.preview.hide()
							
						End if 
					End if 
					
				Else 
					
					$Æ’.target.hide()
					$Æ’.ios.hide()
					$Æ’.android.hide()
					
					$Æ’.preview.hide()
					
				End if 
			End if 
			
			If (FEATURE.with("dominantColor"))
				
				If (PROJECT.ui.dominantColor#Null:C1517)
					
					$Æ’.mainColor:=cs:C1710.color.new(PROJECT.ui.dominantColor).main
					
				End if 
				
			Else 
				
				$Æ’.dominantColor.hide()
				
			End if 
			
			//______________________________________________________
		: ($e.code=On Timer:K2:25)
			
			$Æ’.checkName(Form:C1466.product.name)
			
			If (FEATURE.with("android"))
				
				$Æ’.displayIcon()
				
			Else 
				
				// Obsolete
				$Æ’.loadIcon()
				
			End if 
			
			If (FEATURE.with("dominantColor"))
				
				$Æ’.color.setColors($Æ’.mainColor; $Æ’.mainColor)
				
				If ($Æ’.iconColor=Null:C1517)
					
					$Æ’.iconColor:=cs:C1710.color.new(cs:C1710.bmp.new(OBJECT Get value:C1743("icon")).getDominantColor()).main
					
				End if 
			End if 
			
			//______________________________________________________
		: ($e.code=On Bound Variable Change:K2:52)
			
			PROJECT.setTarget()
			
			// Update UI
			$Æ’.displayTarget()
			$Æ’.refresh()
			
			//______________________________________________________
	End case 
	
Else   // <== WIDGETS METHOD
	
	$e:=$Æ’.event
	
	Case of 
			
			//==============================================
		: ($Æ’.colorButton.catch($e; On Clicked:K2:4))
			
			var $menu : cs:C1710.menu
			
			$menu:=cs:C1710.menu.new()\
				.append("useTheSystemColorSelector"; "picker")\
				.append("useTheMainColorOfTheIcon"; "fromIcon").enable(cs:C1710.color.new($Æ’.mainColor).main#Num:C11($Æ’.iconColor))\
				.popup($Æ’.colorButton)
			
			If ($menu.selected)
				
				var $color : cs:C1710.color
				
				Case of 
						
						//________________________
					: ($menu.choice="picker")
						
						$color:=cs:C1710.color.new(Select RGB color:C956($Æ’.mainColor))
						
						//________________________
					: ($menu.choice="fromIcon")
						
						$color:=cs:C1710.color.new(cs:C1710.bmp.new(OBJECT Get value:C1743("icon")).getDominantColor())
						$Æ’.iconColor:=$color.main
						
						//________________________
				End case 
				
				$Æ’.mainColor:=$color.main
				PROJECT.ui.dominantColor:=$color.css.components
				PROJECT.save()
				
				$Æ’.color.setColors($Æ’.mainColor; $Æ’.mainColor)
				
			End if 
			
			//==============================================
		: ($Æ’.productName.catch())
			
			Case of 
					
					//______________________________________________________
				: ($e.code=On After Edit:K2:43)
					
					$Æ’.checkName(Get edited text:C655)
					
					//______________________________________________________
				: ($e.code=On Data Change:K2:15)
					
					$Æ’.checkName(Form:C1466.product.name)
					
					// Update bundleIdentifier & navigationTitle
					Form:C1466.product.bundleIdentifier:=Form:C1466.organization.id+"."+formatString("bundleApp"; Form:C1466.product.name)
					Form:C1466.main.navigationTitle:=Form:C1466.product.name
					
					//______________________________________________________
			End case 
			
			//==============================================
		: ($Æ’.icon.catch())
			
			// â—ï¸MANAGED INTO OBJECT METHOD BECAUSE OF THE DRAG AND DROP
			
			//==============================================
		: ($Æ’.iconAction.catch($e; On Clicked:K2:4))\
			 & (FEATURE.with("iconActionMenu"))
			
			$Æ’.iconMenu()
			
			//==============================================
		: ($Æ’.ios.catch())\
			 | ($Æ’.android.catch())
			
			Case of 
					
					//______________________________________________________
				: (Is Windows:C1573)
					
					// <NOTHING MORE TO DO>
					
					//______________________________________________________
				: ($e.code=On Clicked:K2:4)
					
					If (Is macOS:C1572)\
						 & ($e.objectName=$Æ’.android.name)\
						 & Not:C34(Form:C1466.$ios)\
						 & Not:C34(Form:C1466.$android)
						
						// Force iOS
						PROJECT.setTarget(True:C214; "ios")
						
					Else 
						
						PROJECT.setTarget(OBJECT Get value:C1743($e.objectName); $e.objectName)
						
					End if 
					
					// Update UI
					$Æ’.displayTarget()
					$Æ’.displayIcon()
					EDITOR.updateRibbon()
					
					//______________________________________________________
				: ($e.code=On Mouse Enter:K2:33)
					
					// Highlights
					OBJECT SET RGB COLORS:C628(*; $e.objectName; EDITOR.selectedColor)
					
					//______________________________________________________
				: ($e.code=On Mouse Leave:K2:34)
					
					// Restore
					OBJECT SET RGB COLORS:C628(*; $e.objectName; Foreground color:K23:1)
					
					//______________________________________________________
			End case 
			
			//________________________________________
	End case 
End if 