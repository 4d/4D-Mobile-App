//%attributes = {"invisible":true}
// ----------------------------------------------------
// Project method : PRODUCT
// Created 16-9-2020 by Vincent de Lachaux
// ----------------------------------------------------
// Description:
// PRODUCT pannel management
// ----------------------------------------------------
// Declarations
var $e; $∆í : Object

// ----------------------------------------------------
// Initialisations
$∆í:=panel_Definition

// ----------------------------------------------------
If (FORM Event:C1606.objectName=Null:C1517)  // <== FORM METHOD
	
	$e:=panel_Form(On Load:K2:1; On Timer:K2:25; On Bound Variable Change:K2:52)
	
	Case of 
			
			//______________________________________________________
		: ($e.code=On Load:K2:1)
			
			If (FEATURE.with("targetPannel"))
				
				$∆í.os.hide()
				$∆í.preview.hide()
				$∆í.dominantColor.moveVertically(-125)
				
			Else 
				
				If (FEATURE.with("androidBeta"))\
					 | (Bool:C1537(PROJECT.$android))  // üöß
					
					If (Is Windows:C1573)
						
						If (Form:C1466.$ios)
							
							$∆í.android.setPicture("#images/os/Android-32.png")\
								.setBackgroundPicture()\
								.setNumStates(1)
							
							$∆í.preview.show()
							
							$∆í.ios.disable()
							
							$∆í.ios.setPicture("#images/os/iOS-32.png")\
								.setBackgroundPicture()\
								.setNumStates(1)
							
						Else 
							
							$∆í.os.hide()
							$∆í.preview.hide()
							
						End if 
					End if 
					
				Else 
					
					$∆í.target.hide()
					$∆í.ios.hide()
					$∆í.android.hide()
					
					$∆í.preview.hide()
					
				End if 
			End if 
			
			If (FEATURE.with("dominantColor"))
				
				If (PROJECT.ui.dominantColor#Null:C1517)
					
					$∆í.mainColor:=cs:C1710.color.new(PROJECT.ui.dominantColor).main
					
				End if 
				
			Else 
				
				$∆í.dominantColor.hide()
				
			End if 
			
			//______________________________________________________
		: ($e.code=On Timer:K2:25)
			
			$∆í.checkName(Form:C1466.product.name)
			
			If (FEATURE.with("android"))
				
				$∆í.displayIcon()
				
			Else 
				
				// Obsolete
				$∆í.loadIcon()
				
			End if 
			
			If (FEATURE.with("dominantColor"))
				
				$∆í.color.setColors($∆í.mainColor; $∆í.mainColor)
				
				If ($∆í.iconColor=Null:C1517)
					
					$∆í.iconColor:=cs:C1710.color.new(cs:C1710.bmp.new(OBJECT Get value:C1743("icon")).getDominantColor()).main
					
				End if 
			End if 
			
			//______________________________________________________
		: ($e.code=On Bound Variable Change:K2:52)
			
			PROJECT.setTarget()
			
			// Update UI
			$∆í.displayTarget()
			$∆í.refresh()
			
			//______________________________________________________
	End case 
	
Else   // <== WIDGETS METHOD
	
	$e:=$∆í.event
	
	Case of 
			
			//==============================================
		: ($∆í.colorButton.catch($e; On Clicked:K2:4))
			
			var $menu : cs:C1710.menu
			
			$menu:=cs:C1710.menu.new()\
				.append("useTheSystemColorSelector"; "picker")\
				.append("useTheMainColorOfTheIcon"; "fromIcon").enable(cs:C1710.color.new($∆í.mainColor).main#Num:C11($∆í.iconColor))\
				.append("enterAWebColor"; "cssColor")\
				.popup($∆í.colorButton)
			
			If ($menu.selected)
				
				var $color : cs:C1710.color
				
				Case of 
						
						//________________________
					: ($menu.choice="picker")
						
						$color:=cs:C1710.color.new(Select RGB color:C956($∆í.mainColor))
						
						//________________________
					: ($menu.choice="fromIcon")
						
						$color:=cs:C1710.color.new(cs:C1710.bmp.new(OBJECT Get value:C1743("icon")).getDominantColor())
						$∆í.iconColor:=$color.main
						//________________________
					: ($menu.choice="cssColor")
						
						var $requested : Text
						$requested:=Request:C163(Get localized string:C991("enterAWebColor"))
						If (Length:C16($requested)>0)
							$color:=cs:C1710.color.new($requested)
							If ($color.isValid())
								$∆í.iconColor:=$color.main
							Else 
								ALERT:C41(Get localized string:C991("invalidWebColor"))
							End if 
						End if 
						
						//________________________
				End case 
				
				If ($color#Null:C1517)
					If ($color.isValid())
						$∆í.mainColor:=$color.main
						PROJECT.ui.dominantColor:=$color.css.components
						PROJECT.save()
						
						$∆í.color.setColors($∆í.mainColor; $∆í.mainColor)
					End if 
				End if 
				
			End if 
			
			//==============================================
		: ($∆í.productName.catch())
			
			Case of 
					
					//______________________________________________________
				: ($e.code=On After Edit:K2:43)
					
					$∆í.checkName(Get edited text:C655)
					
					//______________________________________________________
				: ($e.code=On Data Change:K2:15)
					
					$∆í.checkName(Form:C1466.product.name)
					
					// Update bundleIdentifier & navigationTitle
					Form:C1466.product.bundleIdentifier:=Form:C1466.organization.id+"."+formatString("bundleApp"; Form:C1466.product.name)
					Form:C1466.main.navigationTitle:=Form:C1466.product.name
					
					//______________________________________________________
			End case 
			
			//==============================================
		: ($∆í.icon.catch())
			
			// ‚ùóÔ∏èMANAGED INTO OBJECT METHOD BECAUSE OF THE DRAG AND DROP
			
			//==============================================
		: ($∆í.iconAction.catch($e; On Clicked:K2:4))\
			 & (FEATURE.with("iconActionMenu"))
			
			$∆í.iconMenu()
			
			//==============================================
		: ($∆í.ios.catch())\
			 | ($∆í.android.catch())
			
			Case of 
					
					//______________________________________________________
				: (Is Windows:C1573)
					
					// <NOTHING MORE TO DO>
					
					//______________________________________________________
				: ($e.code=On Clicked:K2:4)
					
					If (Is macOS:C1572)\
						 & ($e.objectName=$∆í.android.name)\
						 & Not:C34(Form:C1466.$ios)\
						 & Not:C34(Form:C1466.$android)
						
						// Force iOS
						PROJECT.setTarget(True:C214; "ios")
						
					Else 
						
						PROJECT.setTarget(OBJECT Get value:C1743($e.objectName); $e.objectName)
						
					End if 
					
					// Update UI
					$∆í.displayTarget()
					$∆í.displayIcon()
					EDITOR.updateRibbon()
					
					//______________________________________________________
				: ($e.code=On Mouse Enter:K2:33)
					
					// Highlights
					OBJECT SET RGB COLORS:C628(*; $e.objectName; EDITOR.selectedColor)
					
					//______________________________________________________
				: ($e.code=On Mouse Leave:K2:34)
					
					// Restore
					OBJECT SET RGB COLORS:C628(*; $e.objectName; Foreground color:K23:1)
					
					//______________________________________________________
			End case 
			
			//________________________________________
	End case 
End if 