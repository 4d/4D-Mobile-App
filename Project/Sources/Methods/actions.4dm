//%attributes = {"invisible":true}
var $e; $Æ’ : Object

$Æ’:=panel_Definition

If (FORM Event:C1606.objectName=Null:C1517)  // <== FORM METHOD
	
	$e:=panel_Form_common(On Load:K2:1; On Timer:K2:25)
	
	Case of 
			
			//______________________________________________________
		: ($e.code=On Load:K2:1)
			
			androidLimitations(True:C214; "Actions are coming soon for Android")
			
			// This trick remove the horizontal gap
			$Æ’.actions.setScrollbars(0; 2)
			
			// Load project actions
			$Æ’.load()
			
			// Set the initial display
			If (_and(Formula:C1597(Form:C1466.dataModel#Null:C1517); Formula:C1597(Not:C34(OB Is empty:C1297(Form:C1466.dataModel)))))
				
				$Æ’.actions.show()
				$Æ’.noPublishedTable.hide()
				
				$Æ’.add.enable()
				$Æ’.databaseMethod.enable()
				
				If (_and(Formula:C1597(Form:C1466.actions#Null:C1517); Formula:C1597(Form:C1466.actions.length>0)))
					
					// Select last used action (or the first one)
					If ($Æ’.$current#Null:C1517)
						
						var $indx : Integer
						$indx:=Form:C1466.actions.indexOf($Æ’.$current)
						$Æ’.actions.select($indx+1)
						
					Else 
						
						$Æ’.actions.select(1)
						
					End if 
					
					
					//$Æ’.callMeBack("selectParameters")
					
					$Æ’.updateParameters()
					
				End if 
				
				$Æ’.actions.focus()
				
			Else 
				
				$Æ’.actions.hide()
				$Æ’.noPublishedTable.show()
				
				$Æ’.add.disable()
				$Æ’.databaseMethod.disable()
				
			End if 
			
			// Set colors
			$Æ’.dropCursor.setColors(Highlight menu background color:K23:7)
			
			// Preload the icons
			$Æ’.callMeBack("loadActionIcons")
			
			// Give the focus to the actions listbox
			$Æ’.actions.focus()
			
			// Add the events that we cannot select in the form properties ðŸ˜‡
			$Æ’.appendEvents(New collection:C1472(On Alternative Click:K2:36; On Before Data Entry:K2:39))
			
			//______________________________________________________
		: ($e.code=On Timer:K2:25)
			
			androidLimitations(True:C214)
			
			$Æ’.update()
			
			//______________________________________________________
	End case 
	
Else   // <== WIDGETS METHOD
	
	$e:=$Æ’.event
	
	Case of 
			
			//==============================================
		: ($Æ’.actions.catch())
			
			Case of 
					
					//_____________________________________
				: ($e.code=On Selection Change:K2:29)
					
					$Æ’.$current:=$Æ’.current
					
					// Update parameters panel
					$Æ’.updateParameters()
					
					// Update UI
					$Æ’.refresh()
					
					//_____________________________________
				: ($e.code=On Getting Focus:K2:7)
					
					$Æ’.actions.setColors(Foreground color:K23:1)
					$Æ’.actionsBorder.setColors(EDITOR.selectedColor)
					
					//_____________________________________
				: ($e.code=On Losing Focus:K2:8)
					
					If (Bool:C1537($Æ’.$edit))  // Focus is lost after editing a cell
						
						OB REMOVE:C1226($Æ’; "$edit")
						
					Else 
						
						$Æ’.actions.setColors(Foreground color:K23:1)
						$Æ’.actionsBorder.setColors(EDITOR.backgroundUnselectedColor)
						
					End if 
					
					//_____________________________________
				: (editor_Locked) | (Num:C11($e.row)=0)
					
					// <NOTHING MORE TO DO>
					
					//_____________________________________
				: ($e.code=On Clicked:K2:4)
					
					Case of 
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
						: ($e.columnName="icons")
							
							$Æ’.doShowIconPicker()
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
					End case 
					
					//_____________________________________
				: ($e.code=On Begin Drag Over:K2:44)
					
					$Æ’.doBeginDrag()
					
					//_____________________________________
				: ($e.code=On Drop:K2:12)
					
					$Æ’.doOnDrop()
					
					//_____________________________________
				: ($e.code=On Before Data Entry:K2:39)
					
					Case of 
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
						: (New collection:C1472("names"; "shorts"; "labels").indexOf($e.columnName)#-1)
							
							// Set a flag to manage On Losing Focus
							$Æ’.$edit:=True:C214
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
						: ($e.columnName="tables")
							
							$Æ’.doTableMenu()
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
						: ($e.columnName="scopes")
							
							$Æ’.doScopeMenu()
							
							//â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦â€¦
					End case 
					
					//_____________________________________
				: ($e.code=On Double Clicked:K2:5)
					
					If (New collection:C1472("names"; "shorts"; "labels").indexOf($e.columnName)#-1)
						
						EDIT ITEM:C870(*; $e.columnName; Num:C11($Æ’.index))
						
					End if 
					
					//_____________________________________
				: ($e.code=On Mouse Leave:K2:34)
					
					$Æ’.dropCursor.hide()
					
					//_____________________________________
			End case 
			
			//==============================================
		: ($Æ’.add.catch())
			
			Case of 
					
					//_____________________________
				: ($e.code=On Alternative Click:K2:36)
					
					$Æ’.doAddMenu()
					
					//_____________________________
				: ($e.code=On Clicked:K2:4)
					
					$Æ’.doNewAction()
					
					//_____________________________
			End case 
			
			//==============================================
		: ($Æ’.remove.catch())
			
			$Æ’.doRemoveAction()
			
			//==============================================
		: ($Æ’.databaseMethod.catch())
			
			$Æ’.doOpenDatabaseMethod()
			
			//==============================================
	End case 
End if 
