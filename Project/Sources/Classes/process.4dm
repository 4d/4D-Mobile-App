Class constructor($idProcess : Variant)
	
	var $name : Text
	var $id; $mode; $origin; $state : Integer
	var $time : Time
	
	If (Count parameters:C259>=1)
		
		Case of 
				
				//______________________________________________________
			: (Value type:C1509($idProcess)=Is longint:K8:6)\
				 | (Value type:C1509($idProcess)=Is real:K8:4)
				
				This:C1470.number:=$idProcess
				
				//______________________________________________________
			: (Value type:C1509($idProcess)=Is text:K8:3)
				
				This:C1470.number:=Process number:C372($idProcess)
				
				//______________________________________________________
			Else 
				
				ASSERT:C1129(False:C215; "Parameter must be a process number or name!")
				
				//______________________________________________________
		End case 
		
	Else 
		
		This:C1470.number:=Current process:C322
		
	End if 
	
	If (This:C1470.number#Null:C1517)
		
		If (This:C1470.number#0)
			
			PROCESS PROPERTIES:C336(This:C1470.number; $name; $state; $time; $mode; $id; $origin)
			
			This:C1470.name:=$name
			This:C1470.state:=$state
			This:C1470.time:=$time
			This:C1470.mode:=$mode
			This:C1470.id:=$id
			This:C1470.origin:=$origin
			This:C1470.hiden:=This:C1470.isHidden()
			This:C1470.preemptive:=This:C1470.isPreemptive()
			This:C1470.cooperative:=Not:C34(This:C1470.preemptive)
			This:C1470.worker:=This:C1470.isWorker()
			This:C1470.web:=This:C1470.isWeb()
			
		Else 
			
			This:C1470.name:=""
			This:C1470.state:=Does not exist:K13:3
			This:C1470.time:=0
			This:C1470.mode:=0
			This:C1470.id:=0
			This:C1470.origin:=0
			
		End if 
	End if 
	
	//=================================================================
Function isPreemptive() : Boolean
	return (This:C1470.mode ?? 1)
	
	//=================================================================
Function isCooperative() : Boolean
	return Not:C34(This:C1470.isPreemptive())
	
	//=================================================================
Function isUser() : Boolean  // Process generated by the user
	return (This:C1470.origin>0)
	
	//=================================================================
Function isInternal() : Boolean  //4Dâ€™s internal process
	return (This:C1470.origin<0)
	
	//=================================================================
Function isWorker() : Boolean
	return (This:C1470.origin=Worker process:K36:32)
	
	//=================================================================
Function isWeb() : Boolean
	return (This:C1470.name="Web Process#@")
	
	//=================================================================
Function isMacro() : Boolean
	return (This:C1470.name="Macro_Call")
	
	//=================================================================
Function isFrontmost($withFloating : Boolean) : Boolean
	If ($withFloating)
		
		return (This:C1470.number=Frontmost process:C327(*))
		
	Else 
		
		return (This:C1470.number=Frontmost process:C327)
		
	End if 
	
	//=================================================================
Function isVisible() : Boolean
	This:C1470.__update()
	return (This:C1470.mode ?? 0)
	
	//=================================================================
Function isHidden() : Boolean
	return Not:C34(This:C1470.isVisible())
	
	//=================================================================
Function doesNotExist() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Does not exist:K13:3)
	
	//=================================================================
Function isAborted() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Aborted:K13:1)
	
	//=================================================================
Function isExecuting() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Executing:K13:4)
	
	//=================================================================
Function isDelayed() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Delayed:K13:2)
	
	//=================================================================
Function isWaiting() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Waiting for user event:K13:9)\
		 | (This:C1470.state=Waiting for input output:K13:7)\
		 | (This:C1470.state=Waiting for internal flag:K13:8)
	
	//=================================================================
Function isPaused() : Boolean
	This:C1470.__update()
	return (This:C1470.state=Paused:K13:6)
	
	//=================================================================
	// Brings all the windows belonging to the process to the front
Function frontmost
	BRING TO FRONT:C326(This:C1470.number)
	
	//=================================================================
	// Displays all the windows belonging to the process
Function show : Object
	SHOW PROCESS:C325(This:C1470.number)
	
	return This:C1470
	
	//=================================================================
	// Hides all windows that belong to theprocess
Function hide : Object
	HIDE PROCESS:C324(This:C1470.number)
	
	return This:C1470
	
	//=================================================================
	// Delays the execution of the process for a number of ticks (1 tick = 1/60th of a second)
Function delay($delay : Integer) : Object
	DELAY PROCESS:C323(This:C1470.number; $delay)
	
	return This:C1470
	
	//=================================================================
	// Suspends the execution of the process until it is reactivated
Function pause : Object
	PAUSE PROCESS:C319(This:C1470.number)
	return This:C1470
	
	//=================================================================
	// Resumes a process whose execution has been paused or delayed
Function resume($show : Boolean) : Object
	RESUME PROCESS:C320(This:C1470.number)
	
	If ($show)
		
		SHOW PROCESS:C325(This:C1470.number)
		
	End if 
	
	return This:C1470
	
	//=================================================================
Function __update
	var $name : Text
	var $mode; $state : Integer
	var $time : Time
	
	PROCESS PROPERTIES:C336(This:C1470.number; $name; $state; $time; $mode)
	
	This:C1470.state:=$state
	This:C1470.time:=$time
	This:C1470.mode:=$mode