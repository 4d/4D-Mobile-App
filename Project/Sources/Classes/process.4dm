Class constructor
	var $1: Variant
	
	var $name : Text
	var $id; $mode; $origin; $state : Integer
	var $time : Time
	
	If (Count parameters:C259>=1)
		
		Case of 
				
				//______________________________________________________
			: (Value type:C1509($1)=Is real:K8:4)
				
				This:C1470.number:=$1
				
				//______________________________________________________
			: (Value type:C1509($1)=Is text:K8:3)
				
				This:C1470.number:=Process number:C372($1)
				
				//______________________________________________________
			Else 
				
				ASSERT:C1129(False:C215; "Parameter must be a process number or name!")
				
				//______________________________________________________
		End case 
		
	Else 
		
		This:C1470.number:=Current process:C322
		
	End if 
	
	If (This:C1470.number#Null:C1517)
		
		If (This:C1470.number#0)
			
			PROCESS PROPERTIES:C336(This:C1470.number; $name; $state; $time; $mode; $id; $origin)
			
			This:C1470.name:=$name
			This:C1470.state:=$state
			This:C1470.time:=$time
			This:C1470.mode:=$mode
			This:C1470.id:=$id
			This:C1470.origin:=$origin
			This:C1470.hiden:=This:C1470.isHidden()
			This:C1470.preemptive:=This:C1470.isPreemptive()
			This:C1470.cooperative:=Not:C34(This:C1470.preemptive)
			This:C1470.worker:=This:C1470.isWorker()
			This:C1470.web:=This:C1470.isWeb()
			
		Else 
			
			This:C1470.name:=""
			This:C1470.state:=Does not exist:K13:3
			This:C1470.time:=0
			This:C1470.mode:=0
			This:C1470.id:=0
			This:C1470.origin:=0
			
		End if 
	End if 
	
	//=================================================================
Function isPreemptive
	var $0: Boolean
	
	$0:=(This:C1470.mode ?? 1)
	
	//=================================================================
Function isCooperative
	var $0: Boolean
	
	$0:=Not:C34(This:C1470.isPreemptive())
	
	//=================================================================
Function isUser  // Process generated by the user
	var $0: Boolean
	
	$0:=(This:C1470.origin>0)
	
	//=================================================================
Function isInternal  //4Dâ€™s internal process
	var $0: Boolean
	
	$0:=(This:C1470.origin<0)
	
	//=================================================================
Function isWorker
	var $0: Boolean
	
	$0:=(This:C1470.origin=Worker process:K36:32)
	
	//=================================================================
Function isWeb
	var $0: Boolean
	
	$0:=(This:C1470.name="Web Process#@")
	
	//=================================================================
Function isMacro
	var $0: Boolean
	
	$0:=(This:C1470.name="Macro_Call")
	
	//=================================================================
Function isFrontmost
	var $0: Boolean
	var $1: Boolean
	
	var $withFloating : Boolean
	
	If (Count parameters:C259>=1)
		
		$withFloating:=$1
		
	End if 
	
	If ($withFloating)
		
		$0:=(This:C1470.number=Frontmost process:C327(*))
		
	Else 
		
		$0:=(This:C1470.number=Frontmost process:C327)
		
	End if 
	
	//=================================================================
Function isVisible
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.mode ?? 0)
	
	//=================================================================
Function isHidden
	var $0: Boolean
	
	$0:=Not:C34(This:C1470.isVisible())
	
	//=================================================================
Function doesNotExist
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Does not exist:K13:3)
	
	//=================================================================
Function isAborted
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Aborted:K13:1)
	
	//=================================================================
Function isExecuting
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Executing:K13:4)
	
	//=================================================================
Function isDelayed
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Delayed:K13:2)
	
	//=================================================================
Function isWaiting
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Waiting for user event:K13:9)\
		 | (This:C1470.state=Waiting for input output:K13:7)\
		 | (This:C1470.state=Waiting for internal flag:K13:8)
	
	//=================================================================
Function isPaused
	var $0: Boolean
	
	This:C1470.__update()
	$0:=(This:C1470.state=Paused:K13:6)
	
	//=================================================================
	// Brings all the windows belonging to the process to the front
Function frontmost
	
	BRING TO FRONT:C326(This:C1470.number)
	
	//=================================================================
	// Displays all the windows belonging to the process
Function show
	var $0: Object
	
	SHOW PROCESS:C325(This:C1470.number)
	
	$0:=This:C1470
	
	//=================================================================
	// Hides all windows that belong to theprocess
Function hide
	var $0: Object
	
	HIDE PROCESS:C324(This:C1470.number)
	
	$0:=This:C1470
	
	//=================================================================
	// Delays the execution of the process for a number of ticks (1 tick = 1/60th of a second)
Function delay
	var $0: Object
	var $1: Integer
	
	var $delay : Integer
	
	If (Count parameters:C259>=1)
		
		$delay:=$1
		
	End if 
	
	DELAY PROCESS:C323(This:C1470.number; $delay)
	
	$0:=This:C1470
	
	//=================================================================
	// Suspends the execution of the process until it is reactivated
Function pause
	var $0: Object
	
	PAUSE PROCESS:C319(This:C1470.number)
	
	$0:=This:C1470
	
	//=================================================================
	// Resumes a process whose execution has been paused or delayed
Function resume
	var $0: Object
	var $1: Boolean
	
	RESUME PROCESS:C320(This:C1470.number)
	
	If (Count parameters:C259>=1)
		
		If ($1)
			
			SHOW PROCESS:C325(This:C1470.number)
			
		End if 
	End if 
	
	$0:=This:C1470
	
	//=================================================================
Function __update
	var $name : Text
	var $mode; $state : Integer
	var $time : Time
	
	PROCESS PROPERTIES:C336(This:C1470.number; $name; $state; $time; $mode)
	
	This:C1470.state:=$state
	This:C1470.time:=$time
	This:C1470.mode:=$mode