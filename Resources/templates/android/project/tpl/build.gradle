// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    project.ext.set("sdkPath", "{{cache_4d_sdk_path}}")
    dependencies {
        classpath Tools.gradle
        classpath Tools.kotlin_gradle_plugin
        classpath Tools.navigation_safe_args_gradle_plugin
        classpath Tools.artifactory
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
    repositories {
        google()
        jcenter()
        maven { url file(project.sdkPath) }
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.36.0"
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"
    repositories {
        maven {
            def machineIP = System.getenv('ARTIFACTORY_MACHINE_IP')
            def contextUrl = "http://${machineIP}:8081/artifactory"
            def repoKey = "libs-release-local"
            url "${contextUrl}/${repoKey}"
            credentials {
                username = System.getenv('ARTIFACTORY_USERNAME')
                password = System.getenv('ARTIFACTORY_PASSWORD')
            }
        }
        google()
        jcenter()
        maven { url file(project.sdkPath) }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion Config.compileSdk
                buildToolsVersion Config.buildTools

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    versionCode 1
                    versionName "1.0"
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    vectorDrawables.useSupportLibrary = true

                    javaCompileOptions {
                        annotationProcessorOptions {
                            arguments = [
                                    "room.schemaLocation": "$projectDir/schemas".toString()
                            ]
                        }
                    }
                }

                buildTypes {

                    debug {
                        testCoverageEnabled true
                    }

                    release {
                        // Enables code shrinking, obfuscation, and optimization for only
                        // your project's release build type.
//                        minifyEnabled true

                        // Enables resource shrinking, which is performed by the
                        // Android Gradle plugin.
//                        shrinkResources true

                        // Includes the default ProGuard rules files that are packaged with
                        // the Android Gradle plugin. To learn more, go to the section about
                        // R8 configuration files.
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
                configurations.all {
                    // Check for updates every build
                    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
                }
                testOptions {
                    unitTests.includeAndroidResources = true
                }
                packagingOptions {
                    exclude 'META-INF/atomicfu.kotlin_module'
                }
                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }
                kotlinOptions {
                    jvmTarget = "1.8"
                }
                dataBinding {
                    enabled = true
                }
                lintOptions {
                    warning 'InvalidPackage'
                    abortOnError false
                }
            }
        }
    }
}